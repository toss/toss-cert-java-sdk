plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.github.toss'
version '0.0.14'

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

jar { enabled = true }

shadowJar {
    minimize()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = uri("https://jitpack.io") }
}

dependencies {
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'javax.json:javax.json-api:1.1.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
}

task javaJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.output
}

task javaSourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives shadowJar
    archives javaSourcesJar
    archives javaDocJar
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java

            version = version
            artifactId = project.name
            groupId = rootProject.group

            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
